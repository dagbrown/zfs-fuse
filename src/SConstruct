import os, sys, re

install_dir = '/usr/local/sbin'

env = Environment()

env['CC'] = 'gcc'
env['LINKFLAGS'] = Split('-pipe -Wall -Werror')
env['CCFLAGS'] = Split('-pipe -Wall -Werror -std=c99 -Wno-missing-braces -Wno-parentheses -Wno-uninitialized -fno-strict-aliasing -D_GNU_SOURCE -D_FILE_OFFSET_BITS=64 -D_REENTRANT -DTEXT_DOMAIN=\\"zfs-fuse\\"')

debug = int(ARGUMENTS.get('debug', '1'))

if not debug:
	env.Append(LINKFLAGS = Split('-s'))
	env.Append(CCFLAGS = Split('-s -O2 -DNDEBUG'))
else:
	env.Append(LINKFLAGS = Split('-ggdb'))
	env.Append(CCFLAGS = Split('-ggdb -DDEBUG'))
	if debug == 1:
		env.Append(CCFLAGS = Split('-O2'))
	elif debug == 3:
		env.Append(CCFLAGS = Split('-finstrument-functions'))

env['CPPPATH'] = []

f = os.popen('uname -m')
arch = f.readline().strip()
f.close()

def getarch(arch):
	if arch == 'x86_64':
		return 'amd64'

	if re.compile('i\d86').match(arch):
		return 'i386'

	return None

myarch = getarch(arch)

if not myarch:
	print
	print 'Sorry, only the x86 and amd64 hardware architectures are supported'
	sys.exit(1)

env['ARCH'] = ARGUMENTS.get('target', myarch)

if env['ARCH'] == 'i386' and myarch == 'amd64':
	env.Append(CCFLAGS = '-m32')
	env.Append(ASFLAGS = '-m32')
	env.Append(LINKFLAGS = '-m32')

Export('env')

SConscript('lib/libavl/SConscript')
SConscript('lib/libsolcompat/SConscript')
SConscript('lib/libnvpair/SConscript')
SConscript('lib/libumem/SConscript')
SConscript('lib/libuutil/SConscript')
SConscript('lib/libzfs/SConscript')
SConscript('lib/libzfscommon/SConscript')
SConscript('lib/libzpool/SConscript')
SConscript('lib/libsolkerncompat/SConscript')
SConscript('cmd/zdb/SConscript')
SConscript('cmd/ztest/SConscript')
SConscript('cmd/zpool/SConscript')
SConscript('cmd/zfs/SConscript')
SConscript('zfs-fuse/SConscript')
SConscript('SConscript-symlinks')

env.Install(install_dir, 'cmd/zdb/zdb')
env.Install(install_dir, 'cmd/ztest/ztest')
env.Install(install_dir, 'cmd/zpool/zpool')
env.Install(install_dir, 'cmd/zfs/zfs')
env.Install(install_dir, 'zfs-fuse/zfs-fuse')

env.Alias('install', install_dir)
